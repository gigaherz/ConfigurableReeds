buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven {url='https://repo.spongepowered.org/repository/maven-public/'}
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    maven {
        url 'https://maven.tterrag.com/'
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
}

version = "2.2.0"
group = "gigaherz.configurablecane" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ConfigurableCane-UNKNOWN"

project.afterEvaluate {
    archivesBaseName = "ConfigurableCane-${project.MC_VERSION}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

mixin {
    add sourceSets.main, "configurablecane.refmap.json"
}

minecraft {
    mappings channel: 'official', version: '1.17.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            arg "--mixin=configurablecane.mixins.json"

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            arg "--mixin=configurablecane.mixins.json"

            ideaModule "${project.name}.main"

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.59'
    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "configurablecane",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "configurablecane.mixins.json"
        ])
    }
}

////////////////
// IntelliJ Project Import
// The Mixin annotation process does not have an obfuscation source when running through the IntelliJ compiler,
// thus we have to prevent it from being activated as part of importing this Gradle project into IntelliJ.
if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}
